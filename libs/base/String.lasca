import Array

{-
    Strings are UTF-8 encoded.
    `Char` and `Code Point` mean valid Unicode scalar values, i.e. 0-0xd7ff, 0xe000-0x10ffff
    http://unicode.org/glossary/#unicode_scalar_value 
-}

-- length of UTF-8 encoded byte string
extern def bytesCount(s: String): Int = "bytesLength"
extern def chr(codePoint: Int): String = "codePointToString"
extern def fromCharArray(chars: Array Int): String = "codePointsToString"
extern def charToLower(codePoint: Int): Int = "utf8proc_tolower"
extern def charToUpper(codePoint: Int): Int = "utf8proc_toupper"
extern def charToTitle(codePoint: Int): Int = "utf8proc_totitle"
extern def isValidUnicodeScalar(codePoint: Int): Bool = "utf8proc_codepoint_valid"
extern def iterate(s: String, f: Int -> Bool): Unit = "codePointsIterate"
extern def graphemeIterate(s: String, f: String -> Bool): Unit = "graphemesIterate"
extern def utf8procCategory(c: Int): Int = "utf8proc_category"

data GeneralCategory
        = UppercaseLetter       -- ^ Lu: Letter, Uppercase
        | LowercaseLetter       -- ^ Ll: Letter, Lowercase
        | TitlecaseLetter       -- ^ Lt: Letter, Titlecase
        | ModifierLetter        -- ^ Lm: Letter, Modifier
        | OtherLetter           -- ^ Lo: Letter, Other
        | NonSpacingMark        -- ^ Mn: Mark, Non-Spacing
        | SpacingCombiningMark  -- ^ Mc: Mark, Spacing Combining
        | EnclosingMark         -- ^ Me: Mark, Enclosing
        | DecimalNumber         -- ^ Nd: Number, Decimal
        | LetterNumber          -- ^ Nl: Number, Letter
        | OtherNumber           -- ^ No: Number, Other
        | ConnectorPunctuation  -- ^ Pc: Punctuation, Connector
        | DashPunctuation       -- ^ Pd: Punctuation, Dash
        | OpenPunctuation       -- ^ Ps: Punctuation, Open
        | ClosePunctuation      -- ^ Pe: Punctuation, Close
        | InitialQuote          -- ^ Pi: Punctuation, Initial quote
        | FinalQuote            -- ^ Pf: Punctuation, Final quote
        | OtherPunctuation      -- ^ Po: Punctuation, Other
        | MathSymbol            -- ^ Sm: Symbol, Math
        | CurrencySymbol        -- ^ Sc: Symbol, Currency
        | ModifierSymbol        -- ^ Sk: Symbol, Modifier
        | OtherSymbol           -- ^ So: Symbol, Other
        | Space                 -- ^ Zs: Separator, Space
        | LineSeparator         -- ^ Zl: Separator, Line
        | ParagraphSeparator    -- ^ Zp: Separator, Paragraph
        | Control               -- ^ Cc: Other, Control
        | Format                -- ^ Cf: Other, Format
        | Surrogate             -- ^ Cs: Other, Surrogate
        | PrivateUse            -- ^ Co: Other, Private Use
        | NotAssigned           -- ^ Cn: Other, Not Assigned

def generalCategory(char: Int): GeneralCategory = match utf8procCategory(char) {
    0  -> NotAssigned
    1  -> UppercaseLetter
    2  -> LowercaseLetter
    3  -> TitlecaseLetter
    4  -> ModifierLetter
    5  -> OtherLetter
    6  -> NonSpacingMark
    7  -> SpacingCombiningMark
    8  -> EnclosingMark
    9  -> DecimalNumber
    10 -> LetterNumber
    11 -> OtherNumber
    12 -> ConnectorPunctuation
    13 -> DashPunctuation
    14 -> OpenPunctuation
    15 -> ClosePunctuation
    16 -> InitialQuote
    17 -> FinalQuote
    18 -> OtherPunctuation
    19 -> MathSymbol
    20 -> CurrencySymbol
    21 -> ModifierSymbol
    22 -> OtherSymbol
    23 -> Space
    24 -> LineSeparator
    25 -> ParagraphSeparator
    26 -> Control
    27 -> Format
    28 -> Surrogate
    29 -> PrivateUse
}

{- 
    Regular Expression stuff. Lasca uses PCRE2 library internally.
-}
data Pattern

extern def compilePattern(pattern: String): Pattern = "lascaCompileRegex"
extern def matchRegex(pattern: Pattern, str: String): Bool = "lascaMatchRegex"
extern def regexReplace(pattern: Pattern, str: String, replacement: String): String = "lascaRegexReplace"

def replace(heystack: String, needle: String, replacement: String) = {
    p = compilePattern(needle);
    regexReplace(p, heystack, replacement)
}

def startsWith(s: String, prefix: String): Bool = {
    -- FIXME: write decent implementation
    p = compilePattern("^${prefix}.*");
    matchRegex(p, s);
}

def endsWith(s: String, suffix: String): Bool = {
    -- FIXME: write decent implementation
    p = compilePattern(".*${suffix}\$");
    matchRegex(p, s);
}

def foreach(s: String, f: Int -> a): Unit = iterate(s, { char -> f(char); true })

def codePointAt(s: String, index: Int): Int = {
    var i = 0;
    var result = -1;
    iterate(s, { char -> 
        if i.getRef < index then {
            i := i.getRef + 1;
            true;
        }
        else {
            result := char;
            false;
        }
    });
    if result.getRef == -1 then die("Index is out of range: ${index}") else result.getRef;
}

def ord(s: String) = codePointAt(s, 0)

def foldl(s: String, zero: a, f: a -> Int -> a): a = {
    var acc = zero;
    iterate(s, { char -> acc := f(acc.getRef, char); true });
    acc.getRef
}

def codePointCount(s: String): Int = foldl(s, 0, { len, c -> len + 1})

def graphemeCount(s: String): Int = {
    var count = 0;
    graphemeIterate(s, { g -> 
        count := count.getRef + 1; 
        true
    });
    count.getRef
}

def compareLength(s: String, length: Int): Int = {
    var i = 0;
    var result = 0;
    iterate(s, { char -> 
        i := i.getRef + 1;
        i.getRef <= length
    });
    runtimeCompare(i.getRef, length);
}

def length(s) = codePointCount(s)

def codePoints(s: String): Array Int = {
    array = makeArray(s.codePointCount, 0);
    foldl(s, 0, { idx, char -> setIndex(array, idx, char); idx + 1 });
    array;
}

def map(s: String, f: Int -> Int): String = {
    array = s.codePoints;
    transform(array, { i, cp -> f(cp) });
    fromCharArray(array);
}

def toLower(s: String): String = if compareLength(s, 1) == 0 then chr(charToLower(ord(s))) else map(s, charToLower)

def toUpper(s: String): String = if compareLength(s, 1) == 0 then chr(charToUpper(ord(s))) else map(s, charToUpper)

def toTitle(s: String): String = if compareLength(s, 1) == 0 then chr(charToTitle(ord(s))) else map(s, charToTitle)

def capitalize(s: String): String = match compareLength(s, 1) {
    -1 -> s -- empty string
    0 -> chr(charToUpper(ord(s))) -- toUpper a single code point
    _ -> {
        array = s.codePoints;
        setIndex(array, 0, charToUpper(array[0]));
        fromCharArray(array);
    }
}

def isDigit(char: Int) = (char - 48) <= 9
def isLetter(char: Int) = let cat = utf8procCategory(char) in 1 <= cat and cat <= 5 -- Letters
def isNumeric(char: Int) = let cat = utf8procCategory(char) in 9 <= cat and cat <= 11 -- Numbers
def isSpace(char: Int) = char == 32 or 9 <= char and char <= 13 or char == 133 or 160 <= char and runtimeCompare(generalCategory(char), Space) == 0

def main() = {
    empty = "";
    test = "Teástuͤ";
    symbol = "uͤ";
    upperT = "T";
    lowerA = "å";
    asdf = "aßdƒ";
    println(toString(codePointAt(test, 0)));
    println(toString(codePointAt(test, 6)));
    String.foreach(test, { cp -> println(cp.toString) });
    println(toString(codePoints(test)));
    println(toString(symbol.ord));
    println("length in codepoints = ${codePointCount(test)}, length in bytes = ${bytesCount(test)}, length in graphemes = ${graphemeCount(test)}");
    res = foldl("12345", 0, { acc, c -> acc + toInt(chr(c)); });
    println(res.toString);
    println("toLower T = ${chr(charToLower(upperT.ord))} ${toLower(test)}");
    println("toUpper å = ${chr(charToUpper(lowerA.ord))} ${toUpper(test)}");
    println("toTitle å = ${chr(charToTitle(lowerA.ord))} ${toTitle(test)}");
    println("capitalize ${capitalize(empty)} ${capitalize(lowerA)} ${capitalize(test)} ${capitalize(asdf)}");
    println("compare ${compareLength(empty, 1)} ${compareLength(empty, 0)} ${compareLength(empty, -1)} ${compareLength(test, 10)} ${compareLength(test, 7)} ${compareLength(test, 0)}");
    println("replace ${replace(test, symbol, upperT)}");
    println("${test} startsWith ${upperT}: ${startsWith(test, upperT)}, endsWith ${symbol}: ${endsWith(test, symbol)}");
    println("${test} startsWith ${symbol}: ${startsWith(test, symbol)}, endsWith ${upperT}: ${endsWith(test, upperT)}");
    println("Code point 123 is valid Unicode Scalar: ${isValidUnicodeScalar(123)}");
    println("Surrogate code point 55296 is valid Unicode Scalar: ${isValidUnicodeScalar(55296)}");
    println("Code point 1114112 is valid Unicode Scalar: ${isValidUnicodeScalar(1114112)}");
    println("0 is digit: ${isDigit("0".ord)}, 9 is digit: ${isDigit("9".ord)}, 'a' is digit: ${isDigit("a".ord)}");
    gc = { s -> generalCategory(ord(s)) };
    println("${gc("1")} ${gc("a")} ${gc("A")} ${gc(" ")} ${gc("≈")} ${gc("€")}");
    println("å is letter ${isLetter("å".ord)}, 1 is letter ${isLetter("1".ord)}");
    println("å is numeric ${isNumeric("å".ord)}, 1 is numeric ${isNumeric("1".ord)}, ¾ is numeric ${isNumeric("¾".ord)}");
    sp = { s -> isSpace(s.ord) };
    println("' ' is space ${sp(" ")}, '\\t' is space ${sp("\t")}, '\\r' is space ${sp("\r")}, '\\n' is space ${sp("\n")}, 'U+0085' is space ${isSpace(133)}");
}
