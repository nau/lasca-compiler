Pi = 3.141592653589793
SolarMass = 4.0 * Pi * Pi
DaysPerYear = 365.24

data Body = Body(x: Ref Float, y: Ref Float, z: Ref Float, vx: Ref Float, vy: Ref Float, vz: Ref Float, mass: Float)

def body(x, y, z, vx, vy, vz, mass) = Body(Ref(x), Ref(y), Ref(z), Ref(vx), Ref(vy), Ref(vz), mass)

Jupiter = body(
     4.84143144246472090e+00,                  -- x
    -1.16032004402742839e+00,                  -- y
    -1.03622044471123109e-01,                  -- z
     1.66007664274403694e-03 * DaysPerYear,    -- vx
     7.69901118419740425e-03 * DaysPerYear,    -- vy
    -6.90460016972063023e-05 * DaysPerYear,    -- vz
     9.54791938424326609e-04 * SolarMass       -- mass
)

Saturn = body(
     8.34336671824457987e+00,
     4.12479856412430479e+00,
    -4.03523417114321381e-01,
    -2.76742510726862411e-03 * DaysPerYear,
     4.99852801234917238e-03 * DaysPerYear,
     2.30417297573763929e-05 * DaysPerYear,
     2.85885980666130812e-04 * SolarMass
)

Uranus = body(
     1.28943695621391310e+01,
    -1.51111514016986312e+01,
    -2.23307578892655734e-01,
     2.96460137564761618e-03 * DaysPerYear,
     2.37847173959480950e-03 * DaysPerYear,
    -2.96589568540237556e-05 * DaysPerYear,
     4.36624404335156298e-05 * SolarMass
)

Neptune = body(
     1.53796971148509165e+01,
    -2.59193146099879641e+01,
     1.79258772950371181e-01,
     2.68067772490389322e-03 * DaysPerYear,
     1.62824170038242295e-03 * DaysPerYear,
    -9.51592254519715870e-05 * DaysPerYear,
     5.15138902046611451e-05 * SolarMass
)

Sun = body(0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SolarMass)

def for(start: Int, end: Int, f: Int -> Int) = {
    if start < end then {
        f(start);
        for(start + 1, end, f);
    } else 0;
}

def offsetMomentum(bodies: [Body]) = {
    var px = 0.0;
    var py = 0.0;
    var pz = 0.0;
    for(0, 5, { i ->
        ibody = bodies[i];
        mass = ibody.mass;
        px := px.getRef + ibody.vx.getRef * mass;
        py := py.getRef + ibody.vy.getRef * mass;
        pz := pz.getRef + ibody.vz.getRef * mass;
        0
    });
    object = bodies[0];
    object.vx := -px.getRef / SolarMass;
    object.vy := -py.getRef / SolarMass;
    object.vz := -pz.getRef / SolarMass;
}

def squared(x: Float, y: Float, z: Float) = x * x + y * y + z * z

def advance(bodies: [Body], dt) = {
    for(0, 5 - 1, { i ->
        ibody = bodies[i];
        imass = ibody.mass;
        for(i + 1, 5, { j ->
            jbody = bodies[j];
            dx = ibody.x.getRef - jbody.x.getRef;
            dy = ibody.y.getRef - jbody.y.getRef;
            dz = ibody.z.getRef - jbody.z.getRef;
            dSquared = squared(dx, dy, dz);
            distance = sqrt(dSquared);
            mag = dt / (dSquared * distance);
            jmass = jbody.mass;
            ibody.vx := ibody.vx.getRef - dx * jmass * mag;
            ibody.vy := ibody.vy.getRef - dy * jmass * mag;
            ibody.vz := ibody.vz.getRef - dz * jmass * mag;

            jbody.vx := jbody.vx.getRef + dx * imass * mag;
            jbody.vy := jbody.vy.getRef + dy * imass * mag;
            jbody.vz := jbody.vz.getRef + dz * imass * mag;
            0
        });
    });
    for(0, 5, { i ->
        body = bodies[i];
        body.x := body.x.getRef + dt * body.vx.getRef;
        body.y := body.y.getRef + dt * body.vy.getRef;
        body.z := body.z.getRef + dt * body.vz.getRef;
        0
    });
    bodies
}

def energy(bodies: [Body]) = {
    var e = 0.0;
    for(0, 5, { i ->
        body = bodies[i];
        e := e.getRef + 0.5 * body.mass * squared(body.vx.getRef, body.vy.getRef, body.vz.getRef);
        for(i + 1, 5, { j ->
            from  = bodies[i];
            to    = bodies[j];
            dx = from.x.getRef - to.x.getRef;
            dy = from.y.getRef - to.y.getRef;
            dz = from.z.getRef - to.z.getRef;
            distance = sqrt(squared(dx, dy, dz));
            e := e.getRef - from.mass * to.mass / distance;
            0
        });
    });
    e.getRef
}

def calculate(bodies: [Body], i) = if i > 0 then calculate(advance(bodies, 0.01), i - 1) else bodies

def main() = {
    -- set to 50000000 for real benchmark
    args = getArgs();
    numIterations = toInt(args[1]);
    bodies = [Sun, Jupiter, Saturn, Uranus, Neptune];
    bodies.offsetMomentum;
    println(bodies.energy.toString);
    calculate(bodies, numIterations);
    println(energy(bodies).toString);
}
